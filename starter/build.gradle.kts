import org.gradle.kotlin.dsl.support.serviceOf
import org.jetbrains.kotlin.gradle.targets.js.nodejs.NodeJsEnvSpec

plugins {
    kotlin("multiplatform")
    kotlin("plugin.js-plain-objects")
}

kotlin {
    js {
        compilerOptions {
            target = "es2015"
        }
        nodejs {
            binaries.executable()
        }
    }

    sourceSets {
        jsMain {
            dependencies {
                implementation(project(":types"))
            }
        }
        jsTest {
            dependencies {
                implementation(kotlin("test"))
            }
        }
    }
}


tasks.register("setup-screeps-arenas") {
    group = "screeps"
    dependsOn("build")

    val execOps = project.serviceOf<ExecOperations>()
    val nodespec = project.the<NodeJsEnvSpec>() // or rootProject.the<NodeJsEnvSpec>() if that's where it lives

    doLast {
        val arenasDir = project.parent!!.file("arenas")
        logger.lifecycle("setup-screeps-arenas: scanning {}", arenasDir.absolutePath)

        val arenaFolders = arenasDir.listFiles()?.filter { it.isDirectory }.orEmpty()
        if (arenaFolders.isEmpty()) {
            logger.warn("No arena subfolders found under {}", arenasDir.absolutePath)
            return@doLast
        }

        val nodeDir = File(nodespec.executable.get()).parentFile
        logger.lifecycle("using node at {}", nodeDir)
        var npm = File(nodeDir.parentFile, "lib/node_modules/npm/bin/npm-cli.js")
        if (!npm.exists()){
            logger.debug("{} does not exist", npm.absolutePath)
            npm = File(nodeDir, "node_modules/npm/bin/npm-cli.js")
        }
        if (!npm.exists()){
            logger.debug("{} does not exist", npm.absolutePath)
            throw GradleException("Could not locate npm-cli.js near bundled node at: ${nodeDir.absolutePath}")
        }

        arenaFolders.forEach { arenaFolder ->
            logger.lifecycle("Setting up arena: {}", arenaFolder.name)

            execOps.exec {
                workingDir = arenaFolder
                executable = nodespec.executable.get()
                args(npm.absolutePath, "install",  "--no-audit")

            }
        }
    }
}

tasks.named("jsProductionExecutableCompileSync"){
    finalizedBy(generateSourceMapRegistry)
}

val generateSourceMapRegistry by tasks.registering {
    group = "screeps"
    description = "Generate ESM SourceMapRegistry.mjs from compiled *.map files"
    dependsOn(tasks.named("compileProductionExecutableKotlinJs"))

    val sourceMapDir = layout.buildDirectory.dir("compileSync/js/main/productionExecutable/kotlin")
    val outFile = project.parent!!.layout.buildDirectory.file("js/packages/screeps-kotlin-arena-starter/kotlin/screeps-kotlin-arena-starter/sourcemaps/SourceMapRegistry.mjs")

    inputs.dir(sourceMapDir)
    outputs.file(outFile)

    doLast {
        val dir = sourceMapDir.get().asFile
        if (!dir.exists()) throw GradleException("Source map directory does not exist: ${dir.absolutePath}")

        val mapFiles = dir
            .walkTopDown()
            .filter { it.isFile && (it.name.endsWith(".mjs.map")) }
            .sortedBy { it.name }
            .toList()

        if (mapFiles.isEmpty()) throw GradleException("No source maps found under: ${dir.absolutePath}")

        fun jsStringLiteral(s: String): String {
            // Safe JS string literal with JSON content; avoids template literals / backticks.
            val sb = StringBuilder(s.length + 16)
            for (ch in s) {
                when (ch) {
                    '\\' -> sb.append("\\\\")
                    '\"' -> sb.append("\\\"")
                    '\n' -> sb.append("\\n")
                    '\r' -> sb.append("\\r")
                    '\t' -> sb.append("\\t")
                    '\u2028' -> sb.append("\\u2028")
                    '\u2029' -> sb.append("\\u2029")
                    else -> {
                        if (ch.code in 0..0x1F) sb.append("\\u%04x".format(ch.code))
                        else sb.append(ch)
                    }
                }
            }
            return "\"$sb\""
        }

        val rawEntries = mapFiles.joinToString(",\n") { f ->
            val key = f.name.removeSuffix(".map") // "SpawnStrike.mjs"
            val json = f.readText(Charsets.UTF_8)
            "  ${jsStringLiteral(key)}: ${jsStringLiteral(json)}"
        }
        val content = """
            // Generated by Gradle task: generateSourceMapRegistry

            export const sourceRoot = "${projectDir.absolutePath}";
            export const rawSourceMaps = Object.freeze({
            $rawEntries
            });
        """.trimIndent() + "\n"
        val out = outFile.get().asFile
        out.parentFile.mkdirs()
        out.writeText(content, Charsets.UTF_8)

        logger.lifecycle("Wrote ${mapFiles.size} map(s) to: ${out.absolutePath}")
    }
}
